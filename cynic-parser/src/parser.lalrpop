use crate::lexer;

use crate::ast::{storage::*, ids::*, writer::AstWriter, *};

grammar<'input>(input: &'input str, ast: &mut AstWriter);

pub TypeSystemDocument: () = {
    <defs:DefinitionAndDescription+> => {}
}

DefinitionAndDescription: () = {
    <description:StringValue?> <def:TypeSystemDefinition> => {
        ast.store_description(def, description)
    }
}

TypeSystemDefinition: DefinitionId = {
    <def:SchemaDefinition> => ast.schema_definition(def),
    <def:ScalarDefinition> => ast.scalar_definition(def),
    <def:ObjectDefinition> => ast.object_definition(def),
    <def:InterfaceDefinition> => ast.interface_definition(def),
    <def:UnionDefinition> => ast.union_definition(def),
    <def:EnumDefinition> => ast.enum_definition(def),
    <def:InputObjectDefinition> => ast.input_object_definition(def),
    extend <def:SchemaDefinition> => ast.schema_extension(def),
    extend <def:ScalarDefinition> => ast.scalar_extension(def),
    extend <def:ObjectDefinition> => ast.object_extension(def),
    extend <def:InterfaceDefinition> => ast.interface_extension(def),
    extend <def:UnionDefinition> => ast.union_extension(def),
    extend <def:EnumDefinition> => ast.enum_extension(def),
    extend <def:InputObjectDefinition> => ast.input_object_extension(def),
    <def:DirectiveDefinition> => ast.directive_definition(def),
}

SchemaDefinition: SchemaDefinition = {
    schema "{" <roots:RootOperationTypeDefinition*> "}" => SchemaDefinition {
        description: None,
        roots,
    }
};

RootOperationTypeDefinition: RootOperationTypeDefinition = {
    query ":" <name:NamedType> => RootOperationTypeDefinition {
        operation_type: OperationType::Query,
        named_type: name
    },
    mutation ":" <name:NamedType> => RootOperationTypeDefinition {
        operation_type: OperationType::Mutation,
        named_type: name
    },
    subscription ":" <name:NamedType> => RootOperationTypeDefinition {
        operation_type: OperationType::Subscription,
        named_type: name
    },
}

ScalarDefinition: ScalarDefinition = {
    <start:@L> scalar
        <name:Name>
        <directives:Directive*>
        <end:@R>
    => ScalarDefinition {
        name,
        description: None,
        directives,
        span: Span::new(start,end)
    }
}

ObjectDefinition: ObjectDefinition = {
    <start:@L> ty
        <name:Name>
        <implements:ImplementsInterfaces?>
        <directives:Directive*>
        <fields:FieldsDefinition?>
        <end:@R>
    => ObjectDefinition {
        name,
        description: None,
        directives,
        implements: implements.unwrap_or_default(),
        fields: ast.field_range(),
        span: Span::new(start,end)
    }
};

ImplementsInterfaces: Vec<StringId> = {
    <interfaces:ImplementsInterfaces> "&" <name:NamedType> => {
        let mut interfaces = interfaces;
        interfaces.push(name);
        interfaces
    },
    implements "&"? <name:NamedType> => {
        vec![name]
    }
}

ImplementItem: StringId = {
    "&" <name:NamedType> => name,
}

FieldsDefinition: Vec<()> = {
    "{" <fields:FieldDefinition+> "}" => fields
};

FieldDefinition: () = {
    <start:@L>
        <description:StringValue?>
        <name:Name>
        <arguments:ArgumentsDefinition?> ":" <ty:Type>
        <directives:Directive*>
        <end:@R>
    => {
        ast.field_definition(FieldDefinition {
            name,
            ty,
            arguments: arguments.unwrap_or_default(),
            description,
            directives,
            span: Span::new(start,end)
        });
    }
};

ArgumentsDefinition: Vec<InputValueDefinitionId> = {
    "(" <arguments:InputValueDefinition+> ")" => arguments,
};

InterfaceDefinition: InterfaceDefinition = {
    <start:@L> interface
        <name:Name>
        <implements:ImplementsInterfaces?>
        <directives:Directive*>
        <fields:FieldsDefinition?>
        <end:@R>
    => InterfaceDefinition {
        name,
        description: None,
        directives,
        implements: implements.unwrap_or_default(),
        fields: ast.field_range(),
        span: Span::new(start,end)
    }
};

UnionDefinition: UnionDefinition = {
    <start:@L> union
        <name:Name>
        <directives:Directive*>
        <members:UnionMemberTypes?>
        <end:@R>
    => UnionDefinition {
        name,
        description: None,
        members: members.unwrap_or_default(),
        directives,
        span: Span::new(start,end)
    }
};

UnionMemberTypes: Vec<StringId> = {
    <members:UnionMemberTypes> "|" <name:NamedType> => {
        let mut members = members;
        members.push(name);
        members
    },
    "=" "|"? <name:NamedType> => {
        vec![name]
    }
}

EnumDefinition: EnumDefinition = {
    <start:@L> "enum"
        <name:Name>
        <directives:Directive*>
        <values:EnumValuesDefinition?>
        <end:@R>
    => EnumDefinition {
        name,
        description: None,
        directives,
        values: values.unwrap_or_default(),
        span: Span::new(start,end)
    }
};

EnumValuesDefinition: Vec<EnumValueDefinitionId> = {
    "{" <values:EnumValueDefinition+> "}" => values
}

EnumValueDefinition: EnumValueDefinitionId = {
    <start:@L>
        <description:StringValue?>
        <value:EnumValue>
        <directives:Directive*>
        <end:@R>
    => ast.enum_value_definition(EnumValueDefinition {
        value,
        description,
        directives,
        span: Span::new(start,end)
    })

}

InputObjectDefinition: InputObjectDefinition = {
    <start:@L> input
        <name:Name>
        <directives:Directive*>
        <fields:InputFieldsDefinition?>
        <end:@R>
    => InputObjectDefinition {
        name,
        description: None,
        directives,
        fields: fields.unwrap_or_default(),
        span: Span::new(start,end)
    }
}

InputFieldsDefinition: Vec<InputValueDefinitionId> = {
    "{" <fields:InputValueDefinition+> "}" => fields
};

DirectiveDefinition: DirectiveDefinition = {
    <start:@L>
        directive
        "@" <name:Name>
        <arguments:ArgumentsDefinition?>
        <repeatable:Repeatable?>
        on
        <locations:DirectiveLocations>
        <end:@R>
    => DirectiveDefinition {
        name,
        description: None,
        arguments: arguments.unwrap_or_default(),
        repeatable: repeatable.unwrap_or_default(),
        locations,
        span: Span::new(start, end)
    }
}

Repeatable: bool = {
    repeatable => true
}

DirectiveLocations: Vec<DirectiveLocation> = {
    <locations:DirectiveLocations> "|" <location:DirectiveLocation> => {
        let mut locations = locations;
        locations.push(location);
        locations
    },
    "|"? <location:DirectiveLocation> => {
        vec![location]
    }
}

DirectiveLocation: DirectiveLocation = {
    <s:Ident> => s.parse().expect("TODO: error handling")
}

InputValueDefinition: InputValueDefinitionId =
    <start:@L>
        <description:StringValue?>
        <name:Name> ":" <ty:Type> <default:DefaultValue?>
        <directives:Directive*>
        <end:@R>
    =>
        ast.input_value_definition(InputValueDefinition {
            name,
            ty,
            description,
            directives,
            default,
            span: Span::new(start, end)
        });


DefaultValue: ValueId = {
    "=" <v:Value> => v
}

Name: StringId = <s:Ident> => ast.ident(s);

NamedType: StringId = <s:Ident> => ast.ident(s);

Type: TypeId = {
    "[" <ty:Type> => ty,
    <name:NamedType> <wrappers:WrappingType*> => ast.type_reference(Type {
        name,
        wrappers: TypeWrappers::from_iter(wrappers)
    })
}

WrappingType: WrappingType = {
    "!" => WrappingType::NonNull,
    "]" => WrappingType::List
}

Value: ValueId = {
    "$" <name:Name> => ast.value(Value::Variable(name)),
    <int:IntegerLiteral> => ast.value(Value::Int(int.parse().unwrap())),
    <float:FloatLiteral> => ast.value(Value::Float(float.parse().unwrap())),
    <s:StringLiteral> => {
        let id = ast.intern_string(s);
        ast.value(Value::String(id))
    },
    <s:BlockStringLiteral> => {
        let id = ast.intern_string(s);
        ast.value(Value::String(id))
    },
    true => ast.value(Value::Boolean(true)),
    false => ast.value(Value::Boolean(false)),
    null => ast.value(Value::Null),
    "[" <values:Value*> "]" => ast.value(Value::List(values)),
    "{" <fields:ObjectField*> "}" => ast.value(Value::Object(fields)),
    <value:EnumValue> => ast.value(Value::Enum(value)),
}

ObjectField: (StringId, ValueId) = {
    <name:Name> ":" <value:Value> => (name, value)
}

StringValue: StringId = {
    <s:StringLiteral> => {
        ast.intern_string(s)
    },
    <s:BlockStringLiteral> => {
        ast.intern_string(s)
    },
}

EnumValue: StringId = {
    <s:RawIdent> => ast.ident(s),
    schema => ast.ident("schema"),
    query => ast.ident("query"),
    ty => ast.ident("type"),
    input => ast.ident("input"),
}

Directive: DirectiveId = {
    "@" <name:Name> <arguments:Arguments?> => ast.directive(Directive {
        name,
        arguments: arguments.unwrap_or_default()
    })
}

Arguments: Vec<ArgumentId> = {
    "(" <arguments:Argument*> ")" => arguments
}

Argument: ArgumentId = {
    <name:Name> ":" <value:Value> => ast.argument(Argument { <> }),
}

Ident: &'input str = {
    <s:RawIdent> => s,
    schema => "schema",
    query => "query",
    mutation => "mutation",
    subscription => "subscription",
    ty => "type",
    input => "input",
    true => "true",
    false => "false",
    null => "null",
    implements => "implements",
    interface => "interface",
    "enum" => "enum",
    union => "union",
    scalar => "scalar",
    extend => "extend",
    directive => "directive",
    repeatable => "repeatable",
    on => "on",
}

extern {
    type Location = usize;
    type Error = lexer::LexicalError;

    enum lexer::Token<'input> {
        "$" => lexer::Token::Dollar,

        ":" => lexer::Token::Colon,

        "{" => lexer::Token::OpenBrace,
        "}" => lexer::Token::CloseBrace,

        "(" => lexer::Token::OpenParen,
        ")" => lexer::Token::CloseParen,

        "[" => lexer::Token::OpenBracket,
        "]" => lexer::Token::CloseBracket,

        "!" => lexer::Token::Exclamation,
        "=" => lexer::Token::Equals,
        "@" => lexer::Token::At,
        "&" => lexer::Token::Ampersand,
        "|" => lexer::Token::Pipe,

        RawIdent => lexer::Token::Identifier(<&'input str>),

        StringLiteral => lexer::Token::StringLiteral(<&'input str>),
        BlockStringLiteral => lexer::Token::BlockStringLiteral(<&'input str>),
        FloatLiteral => lexer::Token::FloatLiteral(<&'input str>),
        IntegerLiteral => lexer::Token::IntegerLiteral(<&'input str>),

        // Would be nice if these could just be aliases of `Identifier` but LARLPOP doesn't
        // seem to support this well: https://github.com/lalrpop/lalrpop/issues/671
        schema => lexer::Token::Schema,
        query => lexer::Token::Query,
        mutation => lexer::Token::Mutation,
        subscription => lexer::Token::Subscription,
        ty => lexer::Token::Type,
        input => lexer::Token::Input,
        true => lexer::Token::True,
        false => lexer::Token::False,
        null => lexer::Token::Null,
        implements => lexer::Token::Implements,
        interface => lexer::Token::Interface,
        "enum" => lexer::Token::Enum,
        union => lexer::Token::Union,
        scalar => lexer::Token::Scalar,
        extend => lexer::Token::Extend,
        directive => lexer::Token::Directive,
        repeatable => lexer::Token::Repeatable,
        on => lexer::Token::On,
    }
}